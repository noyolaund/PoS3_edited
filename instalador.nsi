; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Sistema de ventas gratuito By Parzibyte"
!define URL_PROGRAMA "http://localhost:2106/static/#/login"
!define NOMBRE_EJECUTABLE_PROGRAMA "sublime_pos_3_by_parzibyte.exe"
!define NOMBRE_PLUGIN "plugin_impresoras.exe"
!define UBICACION_PLUGIN ".\plugin\${NOMBRE_PLUGIN}"
!define NOMBRE_EJECUTABLE_DETENER "detener.exe"
!define PRODUCT_VERSION "1.0"
!define PRODUCT_PUBLISHER "Parzibyte"
!define PRODUCT_WEB_SITE "https://parzibyte.me/blog"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${NOMBRE_EJECUTABLE_PROGRAMA}"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_FUNCTION funcionAlTerminar
!insertmacro MUI_PAGE_FINISH

Function funcionAlTerminar
  Exec "$INSTDIR\${NOMBRE_EJECUTABLE_PROGRAMA}"
  Exec "$INSTDIR\${NOMBRE_PLUGIN}"
  ExecShell "open" "${URL_PROGRAMA}"
FunctionEnd

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "Spanish"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "${PRODUCT_NAME} (Instalador).exe"
InstallDir "$APPDATA\${PRODUCT_NAME}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "Principal" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File ".\api\${NOMBRE_EJECUTABLE_PROGRAMA}"
  File ".\api\init.sql"
  File ".\api\esquema_spos_sqlite.sql"
  File ".\api\esquema_negocios_sqlite.sql"
  File ".\detener\${NOMBRE_EJECUTABLE_DETENER}"
  File "${UBICACION_PLUGIN}"
  SetOutPath "$INSTDIR\dist"
  File /r ".\dist\"
  SetOutPath "$INSTDIR"
  CreateShortCut "$APPDATA\Microsoft\Windows\Start Menu\Programs\Startup\${PRODUCT_NAME}.lnk" "$INSTDIR\${NOMBRE_EJECUTABLE_PROGRAMA}"
  CreateShortCut "$APPDATA\Microsoft\Windows\Start Menu\Programs\Startup\${PRODUCT_NAME}_plugin.lnk" "$INSTDIR\${NOMBRE_PLUGIN}"
  CreateShortCut "$DESKTOP\Desinstalar ${PRODUCT_NAME}.lnk" "$INSTDIR\uninst.exe"
  WriteIniStr "$DESKTOP\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${URL_PROGRAMA}"
SectionEnd

Section -AdditionalIcons
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\${NOMBRE_EJECUTABLE_PROGRAMA}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\EjecutablePrincipal.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "La desinstalación de $(^Name) finalizó satisfactoriamente."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "¿Está completamente seguro que desea desinstalar $(^Name) junto con todos sus componentes?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  DetailPrint "Deteniendo servidor..."
  DetailPrint "$INSTDIR\${NOMBRE_EJECUTABLE_DETENER}"
  Exec "$INSTDIR\${NOMBRE_EJECUTABLE_DETENER}"
  Sleep 5000
  DetailPrint "Detenido"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\${NOMBRE_EJECUTABLE_PROGRAMA}"
  Delete "$INSTDIR\${NOMBRE_EJECUTABLE_DETENER}"
  Delete "$INSTDIR\${NOMBRE_PLUGIN}"
  Delete "$INSTDIR\*.db"
  Delete "$INSTDIR\*.log"
  Delete "$INSTDIR\*.csv"
  Delete "$INSTDIR\*.xlsx"
  Delete "$INSTDIR\*.xls"
  Delete "$INSTDIR\*.sql"
  Delete "$INSTDIR\*.sqlite"
  RMDir /r "$INSTDIR\dist"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall.lnk"
  Delete "$DESKTOP\${PRODUCT_NAME}.url"
  DELETE "$DESKTOP\Desinstalar ${PRODUCT_NAME}.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk"
  Delete "$APPDATA\Microsoft\Windows\Start Menu\Programs\Startup\${PRODUCT_NAME}.lnk"
  RMDir "$SMPROGRAMS\${PRODUCT_NAME}"
  RMDir "$INSTDIR"
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd